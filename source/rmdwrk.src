! 15 May 14 - OS & SRP - Created source file for FMO/RMD multistate method
!
!*MODULE RMDWRK  *DECK RMD_BUILD
!> @brief      Routine does main work for RMD method
!>
!> @author     Olaseni Sode and Spencer R. Pruitt
!>             -2014
!>
!> @details    Routine builds and diagonalizes composite Hamiltonian
!>             Builds matrix of state gradients 
!>
!> @date : May 15, 2014 - Olaseni Sode & Spencer R. Pruitt
!>         Created subroutine
!>
      subroutine RMD_BUILD(nstates,coord1d,natoms,atom_name,
     &                     fmo_energies,fmo_gradients,E,Gradients)
       
      implicit none
       
      integer :: i
      integer :: j
      integer :: ik
      integer :: ij
      integer :: nstates
      integer :: natoms
      integer :: IERR

      integer :: atom_name(natoms)
      integer :: iwrk(nstates)
      
      integer, allocatable :: element(:)

      double precision :: E(1)
      double precision :: fmo_energies(30)
      double precision :: fmo_gradients(30*natoms*3)
      double precision :: Gradients(natoms*3)
      double precision :: SCR(nstates,8)
      double precision :: coord1d(natoms*3)
      double precision :: Htmp(nstates,nstates)
      double precision :: HXtmp(nstates,nstates,3*natoms)
      double precision :: VECtmp(nstates)

      double precision, allocatable :: coord(:)
      double precision, allocatable :: EIG(:)
      double precision, allocatable :: VEC(:,:)
      double precision, allocatable :: HX(:,:,:)
      double precision, allocatable :: H(:,:)
      double precision, allocatable :: H1D(:)

      if(.not.allocated(element)) allocate(element(1:natoms))
      if(.not.allocated(coord)) allocate(coord(1:natoms*3))
      if(.not.allocated(VEC)) allocate(VEC(nstates,nstates))
      if(.not.allocated(EIG)) allocate(EIG(nstates))
      if(.not.allocated(H)) allocate(H(nstates,nstates))
      if(.not.allocated(H1D)) allocate(H1D(nstates*nstates))
      if(.not.allocated(HX)) allocate(HX(nstates,nstates,3*natoms))
       
      element(:) = atom_name(:)
      coord(:) = coord1d(:)

      Gradients(:)=0.0D+00
      H(:,:)=0.0D+00
      Htmp(:,:)=0.0D+00
      HX(:,:,:)=0.0D+00
      HXtmp(:,:,:)=0.0D+00
      VEC(:,:)=0.0D+00

      do i = 1 ,nstates
         H(i,i) = fmo_energies(i)
      enddo

      !Compute couplings and repulsions between state energies
      call ComputeCouplings(natoms,nstates,coord,element,Htmp)
      call ComputeRepulsions(natoms,nstates,coord,element,Htmp)

      H(:,:) = H(:,:) + Htmp(:,:)

      !Transform H to a triangular matrix for GLDIAG 
      CALL CPYSQT(H,H1D,NSTATES,1)

      !Diagonalize H
      CALL GLDIAG(NSTATES,NSTATES,NSTATES,H1D,SCR,EIG,VEC,IERR,IWRK)

      E=EIG(1) !Pass the lowest (ground state) energy back to the dynamics
      
      VECtmp(:)=VEC(1,:)

      HX(:,:,:)=0.0D+00

      do I=1,nstates
         do IJ=1,natoms*3
            HX(I,I,IJ)=fmo_gradients(IJ+natoms*3*(I-1))
         enddo
      enddo

      Hxtmp(:,:,:) = HX(:,:,:)

      !Compute coupling and repulsions between state gradients
      call computecouplingsx(natoms,nstates,coord,element,HXtmp)
      call computerepulsionsx(natoms,nstates,coord,element,HXtmp)

      HX(:,:,:) = HXtmp(:,:,:)

      call ComputeHFGrad(nstates,natoms,Gradients,VEC,HX)

      deallocate(element)
      deallocate(coord)
      deallocate(VEC)
      deallocate(EIG)
      deallocate(H)
      deallocate(H1D)
      deallocate(HX)
      
      return
      end
!
!*MODULE RMDWRK  *DECK computecouplings
!> @brief      Compute the coupling energies
!>
!> @author     Olaseni Sode and Spencer R. Pruitt
!>             -2014
!>
!> @details    
!>            
!> @date : May 18, 2014 - Olaseni Sode & Spencer R. Pruitt
!>         Created subroutine
!>      
      subroutine computecouplings(natoms,nstates,coord,
     &                            element,Htmp)
      
      implicit none
      
      !Passed variables
      integer :: natoms
      integer :: nstates

      integer :: element(natoms)

      double precision :: coord(natoms*3)
      double precision :: Htmp(nstates,nstates)
            
      !Temporaray variables
      integer :: i
      integer :: j
      double precision :: ecouple
      
      do i = 1, nstates
         do j = i+1, nstates
            call computecoupling(natoms,coord,element,i,j,ecouple)
            Htmp(i,j) = ecouple
            Htmp(j,i) = ecouple
         enddo
      enddo

      return
      end      
!
!*MODULE RMDWRK  *DECK computecoupling
!> @brief      Compute the coupling energy b/w states I and J 
!>
!> @author     Olaseni Sode and Spencer R. Pruitt
!>             -2014
!>
!> @details    
!>            
!> @date : May 18, 2014 - Olaseni Sode & Spencer R. Pruitt
!>         Created subroutine
!>            
      subroutine computecoupling(natoms,coord,element,
     &                           istate,jstate,ecouple)

      use rmddat

      implicit none
      
      !Parameters
      integer, parameter :: MAX_STATES = 30 !
      integer, parameter :: MAX_HOPS = 1 ! Maximum number of hops in search
      double precision, parameter :: cut_OH  = 4.5353D+00 ! Distance cutoff in state search between O and H atoms
      double precision, parameter :: cut_OH2 = cut_OH * cut_OH !
      double precision, parameter :: AA = 0.0064054303D+00 ! Geometric mean
      
      !Passed variables
      integer :: natoms
      integer :: istate
      integer :: jstate

      integer :: element(natoms)

      double precision :: ecouple

      double precision :: coord(natoms*3)
      
      !Temporary variables
      integer :: i
      integer :: j
      integer :: shared
      integer :: iproton

      double precision :: dx
      double precision :: dy
      double precision :: dz
      double precision :: dd
      double precision :: tmp
      double precision :: erepItoJ
      double precision :: erepJtoI
      
      ecouple = 0.0D+00
      
      !If not shared atom between state I and J, then coupling is zero
      shared = 0
      iproton = -1 !index of shuttling proton between states I and J
      do i = 1, natoms
         if ( element(i)==1 .and. reactive((istate-1)*natoms+i)==1
     &   .and. reactive((jstate-1)*natoms+i)==1 ) then
          !Reactive fragment in state I shares at least one H atom with reactive fragment in state J
            shared = 1
            iproton = i
            exit
         endif
      enddo
      
      if (shared==1) then
         !Must check that I and J reactive fragments are not on the same oxygen,
         !as it is not possible for self-coupling
         !This can occur with bifurcated waters
         do i = 1, natoms
            if ( element(i)==8 .and. reactive((istate-1)*natoms+i)==1
     &      .and. reactive((jstate-1)*natoms+i)==1 ) then
               !Same oxygen. No coupling allowed.
               shared = 0
               exit
           endif
         enddo
      endif
      
      !If passed above, now actually compute the coupling
      if (shared==1) then
         !Compute interstate coupling repulsion for I to J
         erepItoJ = 0.0D+00
         do i = 1, natoms
            if ( element(i)==1 .and. reactive((istate-1)*natoms+i)==1
     &      .and. iproton==i ) then ! reactive H in state I
               do j = 1, natoms
                  if ( element(j)==8.and.
     &            reactive((jstate-1)*natoms+j)==1)then ! reactive O in state J

                  dx = coord(3*(i-1)+1) - coord(3*(j-1)+1)
                  dy = coord(3*(i-1)+2) - coord(3*(j-1)+2)
                  dz = coord(3*(i-1)+3) - coord(3*(j-1)+3)

                  dd =  dx*dx + dy*dy + dz*dz

                     if ( dd < cut_OH2 ) then
                        call Sr(dd, 0.0D+00, cut_OH2, tmp)
                        erepItoJ = erepItoJ + tmp*tmp
                     endif

                  endif
               enddo
            endif
         enddo
        !Compute interstate coupling repulsion for J to I
         erepJtoI = 0.0D+00
         do i = 1, natoms
            if ( element(i)==1.and.
     &      reactive((jstate-1)*natoms+i)==1.and.iproton==i)then ! reactive H in state J
               do j = 1, natoms
                  if ( element(j)==8.and.
     &            reactive((istate-1)*natoms+j)==1)then ! reactive O in state I

                     dx = coord(3*(i-1)+1) - coord(3*(j-1)+1)
                     dy = coord(3*(i-1)+2) - coord(3*(j-1)+2)
                     dz = coord(3*(i-1)+3) - coord(3*(j-1)+3)
 
                     dd =  dx*dx + dy*dy + dz*dz

                     if ( dd < cut_OH2 ) then
                        call Sr(dd, 0.0D+00, cut_OH2, tmp)
                        erepJtoI = erepJtoI + tmp*tmp
                     endif

                  endif
               enddo
            endif
         enddo
      
         !Geometric mean
         ecouple = AA * sqrt( erepItoJ  * erepJtoI )

      endif

      return
      end
!
!*MODULE RMDWRK  *DECK computecouplingsx
!> @brief      Compute the coupling gradients
!>
!> @author     Olaseni Sode and Spencer R. Pruitt
!>             -2014
!>
!> @details    
!>            
!> @date : May 18, 2014 - Olaseni Sode & Spencer R. Pruitt
!>         Created subroutine
!>            
      subroutine computecouplingsx(natoms,nstates,coord,element,
     &                             HXtmp)
 
      use rmddat
     
      implicit none
      
      !Passed variables
      integer :: natoms
      integer :: nstates

      integer :: element(natoms)

      double precision :: coord(natoms*3)
      double precision :: HXtmp(nstates,nstates,3*natoms)
            
      !Temporaray variables
      integer :: i
      integer :: j
      integer :: k
      
      do i = 1, nstates
         do j = i+1, nstates
            call computecouplingX(nstates,natoms,coord,
     &                            element,i,j,HXtmp)
         enddo
      enddo

      return  
      end
!
!*MODULE RMDWRK  *DECK computecouplingX
!> @brief      Compute the coupling energy b/w states I and J
!>
!> @author     Olaseni Sode and Spencer R. Pruitt
!>             -2014
!>
!> @details    
!>            
!> @date : May 18, 2014 - Olaseni Sode & Spencer R. Pruitt
!>         Created subroutine
!>      
      subroutine computecouplingX(nstates,natoms,coord,element,
     &                            istate,jstate,HXtmp)

      use rmddat      

      implicit none
      
      !Parameters
      double precision, parameter :: cut_OH  = 4.5353D+00 ! Distance cutoff in state search between O and H atoms
      double precision, parameter :: cut_OH2 = cut_OH * cut_OH !
      double precision, parameter :: AA = 0.0064054303D+00 ! Geometric mean
      double precision, parameter :: b2a = 0.529177249D+00 ! Bohr to angstroms
      
      !Passed variables
      integer :: natoms
      integer :: nstates
      integer :: istate
      integer :: jstate

      integer :: element(natoms)

      double precision :: coord(natoms*3)
      double precision :: HXtmp(nstates,nstates,3*natoms)
      
      !Temporary variables
      integer :: i
      integer :: j
      integer :: shared
      integer :: iproton

      double precision :: erepItoJ
      double precision :: erepJtoI
      double precision :: d
      double precision :: dx
      double precision :: dy
      double precision :: dz
      double precision :: dd
      double precision :: fx
      double precision :: fy
      double precision :: fz
      double precision :: tmp
      double precision :: tmp1
      double precision :: tmp2
      double precision :: prefactor
      
      !If not shared atom between state I and J, then coupling is zero
      shared = 0
      iproton = -1 !index of shuttling proton between states I and J
      do i = 1, natoms
         if ( element(i)==1.and.reactive((istate-1)*natoms+i)==1
     &   .and.reactive((jstate-1)*natoms+i)==1) then
            !Reactive fragment in I shares at least one H atom with reactive fragment in J
            shared = 1
            iproton = i
            exit
         endif
      enddo
      
      if (shared==1) then
         !Must check that state I and state J reactive fragments are not on the same oxygen
         !as it is not possible for self-coupling
         !This can occur with bifurcated waters
         do i = 1, natoms
            if ( element(i)==8.and.reactive((istate-1)*natoms+i)==1
     &      .and. reactive((jstate-1)*natoms+i)==1) then
               !Same oxygen. No coupling allowed.
               shared = 0
               exit
            endif
         enddo
      endif
      
      ! ** If passed above, now actually compute the coupling **
      if (shared==1) then
         !Compute interstate coupling repulsion
         erepItoJ = 0.0D+00
         do i = 1, natoms
            if ( element(i)==1.and.reactive((istate-1)*natoms+i)==1
     &      .and.iproton==i) then ! reactive H in state I
               do j = 1, natoms
                  if ( element(j)==8.and.
     &            reactive((jstate-1)*natoms+j)==1) then ! reactive O in state J

                     dx = coord(3*(i-1)+1) - coord(3*(j-1)+1)
                     dy = coord(3*(i-1)+2) - coord(3*(j-1)+2)
                     dz = coord(3*(i-1)+3) - coord(3*(j-1)+3)

                     dd =  dx*dx + dy*dy + dz*dz

                     if ( dd < cut_OH2 ) then
                        call Sr(dd, 0.0D+00, cut_OH2, tmp)
                        erepItoJ = erepItoJ + tmp*tmp
                     endif

                  endif
               enddo
            endif
         enddo
      
         erepJtoI = 0.0D+00
         do i = 1, natoms
            if ( element(i)==1.and.reactive((jstate-1)*natoms+i)==1
     &      .and. iproton==i ) then ! reactive H in state J
               do j = 1, natoms
                  if ( element(j)==8.and.
     &            reactive((istate-1)*natoms+j)==1  ) then ! reactive O in state I

                     dx = coord(3*(i-1)+1) - coord(3*(j-1)+1)
                     dy = coord(3*(i-1)+2) - coord(3*(j-1)+2)
                     dz = coord(3*(i-1)+3) - coord(3*(j-1)+3)

                     dd =  dx*dx + dy*dy + dz*dz
                     if ( dd < cut_OH2 ) then
                        call Sr(dd, 0.0D+00, cut_OH2, tmp)
                        erepJtoI = erepJtoI + tmp*tmp
                     endif

                  endif
               enddo
            endif
         enddo

         prefactor = AA / ( 2.0D+00 * sqrt (erepItoJ * erepJtoI) )
      
        !Now get the gradients
         do i = 1, natoms
            if ( element(i)==1.and.reactive((istate-1)*natoms+i)==1
     &      .and. iproton==i ) then ! reactive H in state I
               do j = 1, natoms
                  if ( element(j)==8.and.
     &            reactive((jstate-1)*natoms+j)==1 ) then ! reactive O in state J

                     dx = coord(3*(i-1)+1) - coord(3*(j-1)+1)
                     dy = coord(3*(i-1)+2) - coord(3*(j-1)+2)
                     dz = coord(3*(i-1)+3) - coord(3*(j-1)+3)

                     dd =  dx*dx + dy*dy + dz*dz

                     if ( dd < cut_OH2 ) then
                        d = sqrt(dd)
                        call Sr(dd, 0.0D+00, cut_OH2, tmp1)
                        call dSrdr(dd, 0.0D+00, cut_OH2, tmp2)
                        tmp = 2.0D+00 * tmp1 * tmp2
                        tmp = tmp * prefactor * erepJtoI
      
                        fx = tmp * dx / d
                        fy = tmp * dy / d
                        fz = tmp * dz / d
      
      HXtmp(istate,jstate,3*(i-1)+1) = HXtmp(istate,jstate,3*(i-1)+1)+fx
      HXtmp(istate,jstate,3*(i-1)+2) = HXtmp(istate,jstate,3*(i-1)+2)+fy
      HXtmp(istate,jstate,3*(i-1)+3) = HXtmp(istate,jstate,3*(i-1)+3)+fz

      HXtmp(istate,jstate,3*(j-1)+1) = HXtmp(istate,jstate,3*(j-1)+1)-fx
      HXtmp(istate,jstate,3*(j-1)+2) = HXtmp(istate,jstate,3*(j-1)+2)-fy
      HXtmp(istate,jstate,3*(j-1)+3) = HXtmp(istate,jstate,3*(j-1)+3)-fz

                     endif
                  endif
               enddo
            endif
         enddo

         do i = 1, natoms
            if ( element(i)==1.and.reactive((jstate-1)*natoms+i)==1
     &      .and. iproton==i ) then ! reactive H in state J
               do j = 1, natoms
                  if ( element(j)==8.and.
     &            reactive((istate-1)*natoms+j)==1 ) then ! reactive O in state I

                     dx = coord(3*(i-1)+1) - coord(3*(j-1)+1)
                     dy = coord(3*(i-1)+2) - coord(3*(j-1)+2)
                     dz = coord(3*(i-1)+3) - coord(3*(j-1)+3)

                     dd =  dx*dx + dy*dy + dz*dz

                     if ( dd < cut_OH2 ) then
                        d = sqrt(dd)
                        call Sr(dd, 0.0D+00, cut_OH2, tmp1)
                        call dSrdr(dd, 0.0D+00, cut_OH2, tmp2)
                        tmp = 2.0D+00 * tmp1 * tmp2
                        tmp = tmp * prefactor * erepItoJ
      
                        fx = tmp * dx / d
                        fy = tmp * dy / d
                        fz = tmp * dz / d
                        
      HXtmp(istate,jstate,3*(i-1)+1) = HXtmp(istate,jstate,3*(i-1)+1)+fx
      HXtmp(istate,jstate,3*(i-1)+2) = HXtmp(istate,jstate,3*(i-1)+2)+fy
      HXtmp(istate,jstate,3*(i-1)+3) = HXtmp(istate,jstate,3*(i-1)+3)+fz
      
      HXtmp(istate,jstate,3*(j-1)+1) = HXtmp(istate,jstate,3*(j-1)+1)-fx
      HXtmp(istate,jstate,3*(j-1)+2) = HXtmp(istate,jstate,3*(j-1)+2)-fy
      HXtmp(istate,jstate,3*(j-1)+3) = HXtmp(istate,jstate,3*(j-1)+3)-fz
                     endif
                  endif
               enddo
            endif
         enddo
      
         !Symmetrize coupling gradient for convience
         do i = 1, natoms
      HXtmp(jstate,istate,3*(i-1)+1) = HXtmp(istate,jstate,3*(i-1)+1)
      HXtmp(jstate,istate,3*(i-1)+2) = HXtmp(istate,jstate,3*(i-1)+2)
      HXtmp(jstate,istate,3*(i-1)+3) = HXtmp(istate,jstate,3*(i-1)+3)
         enddo

      endif
      
      return      
      end

!
!*MODULE RMDWRK  *DECK computerepulsions
!> @brief      Compute the repulsions energies
!>
!> @author     Olaseni Sode and Spencer R. Pruitt
!>             -2014
!>
!> @details
!>
!> @date : June 9, 2014 - Olaseni Sode & Spencer R. Pruitt
!>         Created subroutine
!
      subroutine computerepulsions(natoms,nstates,coord,
     &                            element,Htmp)

      implicit none

      !Passed variables
      integer :: natoms
      integer :: nstates

      integer :: element(natoms)

      double precision :: coord(natoms*3)
      double precision :: Htmp(nstates,nstates)

      !Temporaray variables
      integer :: i
      double precision :: erepulsion

      do i = 1, nstates
        call computerepulsion(natoms,coord,element,i,erepulsion)
        Htmp(i,i) = Htmp(i,i) + erepulsion
      enddo

      return
      end

!
!*MODULE RMDWRK  *DECK computerepulsion
!> @brief      Compute the repulsion energy of states I
!>
!> @author     Olaseni Sode and Spencer R. Pruitt
!>             -2014
!>
!> @details
!>
!> @date : June 9, 2014 - Olaseni Sode & Spencer R. Pruitt
!>         Created subroutine
!>
      subroutine computerepulsion(natoms,coord,element,
     &                           istate,erepulsion)

      use rmddat

      implicit none

      !Parameters
      integer, parameter :: MAX_STATES = 30 !
      integer, parameter :: MAX_HOPS = 1 ! Maximum number of hops in search
      double precision, parameter :: cut_OH  = 4.5353D+00 ! Distance cutoff in state search between O and H atoms
      double precision, parameter :: cut_OH2 = cut_OH * cut_OH !
      double precision, parameter :: AA = 0.0064054303D+00 ! Geometric mean

      !Passed variables
      integer :: natoms
      integer :: istate

      integer :: element(natoms)

      double precision :: erepulsion

      double precision :: coord(natoms*3)

      !Temporary variables
      integer :: i
      integer :: j

      double precision :: dx
      double precision :: dy
      double precision :: dz
      double precision :: dd
      double precision :: tmp

      erepulsion = 0.0D+00

      !Let each hydronium H atom interact with all non-hydronium O atoms in state istate
      do i = 1, natoms
        if ( element(i)==1 .and. reactive((istate-1)*natoms+i)==1 ) then
          do j = 1, natoms
            if ( element(j)==8 .and.
     &      reactive((istate-1)*natoms+j)==0 ) then

              dx = coord(3*(i-1)+1) - coord(3*(j-1)+1)
              dy = coord(3*(i-1)+2) - coord(3*(j-1)+2)
              dz = coord(3*(i-1)+3) - coord(3*(j-1)+3)

              dd =  dx*dx + dy*dy + dz*dz

              if ( dd < cut_OH2 ) then
                call Sr(dd, 0.0D+00, cut_OH2, tmp)
                erepulsion = erepulsion + tmp*tmp
              endif
            endif
          enddo
        endif
      enddo

      erepulsion = erepulsion * AA
      return
      end


!
!*MODULE RMDWRK  *DECK computerepulsionsx
!> @brief      Compute the repulsion gradients
!>
!> @author     Olaseni Sode and Spencer R. Pruitt
!>             -2014
!>
!> @details
!>
!> @date : June 9, 2014 - Olaseni Sode & Spencer R. Pruitt
!>         Created subroutine
!>
      subroutine computerepulsionsx(natoms,nstates,
     &                              coord,element,HXtmp)

      use rmddat

      implicit none

      !Passed variables
      integer :: natoms
      integer :: nstates

      integer :: element(natoms)

      double precision :: coord(natoms*3)
      double precision :: HXtmp(nstates,nstates,3*natoms)

      !Temporaray variables
      integer :: i

      do i = 1, nstates
        call computerepulsionX(natoms,nstates,coord,element,i,HXtmp)
      enddo

      return
      end


!
!*MODULE RMDWRK  *DECK computerepulsionX
!> @brief      Compute the repulsion gradients for state I
!>
!> @author     Olaseni Sode and Spencer R. Pruitt
!>             -2014
!>
!> @details
!>
!> @date : June 9, 2014 - Olaseni Sode & Spencer R. Pruitt
!>         Created subroutine
!>
      subroutine computerepulsionX(natoms,nstates,coord,element,
     &                            istate,HXtmp)

      use rmddat

      implicit none

      !Parameters
      double precision, parameter :: cut_OH  = 4.5353D+00 ! Distance cutoff in state search between O and H atoms
      double precision, parameter :: cut_OH2 = cut_OH * cut_OH !
      double precision, parameter :: AA = 0.0064054303D+00 ! Geometric mean
      double precision, parameter :: b2a = 0.529177249D+00 ! Bohr to angstroms

      !Passed variables
      integer :: natoms
      integer :: istate
      integer :: nstates

      integer :: element(natoms)

      double precision :: coord(natoms*3)
      double precision :: HXtmp(nstates,nstates,3*natoms)

      !Temporary variables
      integer :: i
      integer :: j

      double precision :: d
      double precision :: dx
      double precision :: dy
      double precision :: dz
      double precision :: dd
      double precision :: fx
      double precision :: fy
      double precision :: fz
      double precision :: tmp
      double precision :: tmp1
      double precision :: tmp2

      !Let each hydronium H atom interact with all non-hydronium O atoms in state istate
      do i = 1, natoms
        if ( element(i)==1 .and. reactive((istate-1)*natoms+i)==1 ) then
          do j = 1, natoms
            if ( element(j)==8 .and.
     &      reactive((istate-1)*natoms+j)==0 ) then

              dx = coord(3*(i-1)+1) - coord(3*(j-1)+1)
              dy = coord(3*(i-1)+2) - coord(3*(j-1)+2)
              dz = coord(3*(i-1)+3) - coord(3*(j-1)+3)

              dd =  dx*dx + dy*dy + dz*dz

              if ( dd < cut_OH2 ) then
                d = sqrt(dd)
                call Sr(dd, 0.0D+00, cut_OH2, tmp1)
                call dSrdr(dd, 0.0D+00, cut_OH2, tmp2)
                tmp = 2.0D+00 * tmp1 * tmp2
                tmp = tmp * AA
                tmp = tmp * b2a

                fx = tmp * dx / d
                fy = tmp * dy / d
                fz = tmp * dz / d

      HXtmp(istate,istate,3*(i-1)+1)=HXtmp(istate,istate,3*(i-1)+1)+fx
      HXtmp(istate,istate,3*(i-1)+2)=HXtmp(istate,istate,3*(i-1)+2)+fy
      HXtmp(istate,istate,3*(i-1)+3)=HXtmp(istate,istate,3*(i-1)+3)+fz

      HXtmp(istate,istate,3*(j-1)+1)=HXtmp(istate,istate,3*(j-1)+1)-fx
      HXtmp(istate,istate,3*(j-1)+2)=HXtmp(istate,istate,3*(j-1)+2)-fy
      HXtmp(istate,istate,3*(j-1)+3)=HXtmp(istate,istate,3*(j-1)+3)-fz
              endif
            endif
          enddo
        endif
      enddo

      return
      end





!
!*MODULE RMDWRK  *DECK Sr
!> @brief      Switching function
!>
!> @author     Olaseni Sode and Spencer R. Pruitt
!>             -2014
!>
!> @details
!>
!> @date : May 18, 2014 - Olaseni Sode & Spencer R. Pruitt
!>         Created subroutine
!>
      subroutine Sr ( r2, rin2, rout2, output )
      
      implicit none
      
      double precision :: r2
      double precision :: rin2
      double precision :: rout2
      double precision :: output
      double precision :: denom
      double precision :: tmp
      
      if ( r2 < rin2 ) then
         output = 1.0D+00
      else if ( r2 > rout2 ) then
         output = 0.0D+00
      else
         denom = rout2 - rin2
         denom = denom * denom * denom
         tmp = rout2 - r2
         output = tmp*tmp* ( rout2 + 2.0D+00*r2 - 3.0D+00*rin2 ) / denom
      endif

      return
      end
!
!*MODULE RMDWRK  *DECK dSrdr
!> @brief      Switching function derivative
!>
!> @author     Olaseni Sode and Spencer R. Pruitt
!>             -2014
!>
!> @details    
!>            
!> @date : May 18, 2014 - Olaseni Sode & Spencer R. Pruitt
!>         Created subroutine
!>
      subroutine dSrdr ( r2, rin2, rout2, output )
      
      implicit none
      
      double precision :: r2
      double precision :: rin2
      double precision :: rout2
      double precision :: output
      double precision :: denom
      double precision :: tmp
      double precision :: r
      
      if ( r2 < rin2 ) then
         output = 0.0D+00
      else if ( r2 > rout2 ) then
         output = 0.0D+00
      else
         denom = rout2 - rin2
         denom = denom * denom * denom
         tmp = rout2 - r2
         r = sqrt( r2 )
         output = -12.0D+00*r * (rout2 - r2) * (r2 -rin2) / denom
      endif

      return
      end
!
!*MODULE RMDWRK  *DECK ComputeHFGrad
!> @brief      Routine computes composite gradient
!>
!> @author     Olaseni Sode and Spencer R. Pruitt
!>             -2014
!>
!> @details    Routine computes composite gradient via Hellmann-Feynman theorem
!>            
!> @date : May 15, 2014 - Olaseni Sode & Spencer R. Pruitt
!>         Created subroutine
!>
      subroutine ComputeHFGrad(nstates,natoms,
     &           GSGradients,GSCoeffs,HX)
      
      implicit none

      !Passed variables      
      integer :: nstates
      integer :: natoms

      !Temporary variables      
      integer :: i
      integer :: istate
      integer :: jstate

      double precision :: cI
      double precision :: cJ
      double precision :: cc

      double precision :: HX(nstates,nstates,3*natoms)
      double precision :: GSGradients(natoms*3)
      double precision :: GSCoeffs(nstates)

      GSGradients(:)=0.0D+00

      do istate = 1, nstates
         cI = GSCoeffs(istate)
         do jstate = 1, nstates
            cJ = GSCoeffs(jstate)
            cc = cI*cJ
            do i = 1, natoms
               GSGradients(3*(i-1)+1)=GSGradients(3*(i-1)+1)+
     &                                cc*HX(istate,jstate,3*(i-1)+1)

               GSGradients(3*(i-1)+2)=GSGradients(3*(i-1)+2)+
     &                                cc*HX(istate,jstate,3*(i-1)+2)

               GSGradients(3*(i-1)+3)=GSGradients(3*(i-1)+3)+
     &                                cc*HX(istate,jstate,3*(i-1)+3)
            enddo
         enddo
      enddo

      return       
      end
