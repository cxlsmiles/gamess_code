! 18 Mar 14 - OS & SRP - Created source file for FMO/RMD multistate method
!
!*MODULE RMDGEN  *DECK STATE_SEARCH
!> @brief      Routine sets up RMD molecular dynamics runs
!>
!> @author     Olaseni Sode and Spencer R. Pruitt
!>             -2014
!>
!> @details    Routine generates all relevant states
!>
!> @date : March 18, 2014 - Olaseni Sode & Spencer R. Pruitt
!>         Created subroutine
!>
      subroutine state_search (nstates,state,nfrg,hop,hop_length,
     $                        irmddat,irmdchg,coord1d,atom_name,nat)

      use rmddat, only : reactive

      implicit none

      double precision, parameter :: MAX_STATES = 30 !
      double precision, parameter :: MAX_HOPS = 2 ! Maximum number of hops in search
      double precision, parameter :: cut_OH  = 4.5D+00 ! Distance cutoff (Bohr) in state search between O and H atoms
      double precision, parameter :: cut_OH2 = cut_OH * cut_OH !

      integer, allocatable :: charges(:)
      integer, allocatable :: init_charges(:)
      integer, allocatable :: fragment(:)
      integer, allocatable :: old_fragment(:) ! Helper array for checking that fragment indexing has changed or not
      integer, allocatable :: init_fragment(:)
      integer, allocatable :: available(:)
      integer, allocatable :: hop_array(:)
      integer, allocatable :: environment(:)
      integer, allocatable :: toBecomeUnavailableNextHop(:) ! Helper array for keeping track of which oxygens are unavailable
      double precision, allocatable :: coord(:)
      integer, allocatable :: element(:)

      integer :: nat            ! SRP: Number of atoms

      double precision :: coord1d(nat*3)

      integer :: atom_name(nat)
      integer :: irmddat(nat*30)
      integer :: irmdchg(nat*30)

      integer :: natoms
      integer :: MAX_SPACE
      integer :: nprevstates
      integer :: ireactive
      integer :: hop_length     ! How many hop elements we've counted
      integer :: hop            ! Hop level
      integer :: state          ! Current state index from which we are searching
      integer :: nfoundstates
      integer :: nextstate      ! Next state index in search
      integer :: ifrag
      integer :: fragmentIndexChange
      integer :: nstates        ! Number states found
      integer :: nfragments
      integer :: nfrg
      integer :: jfrag
      integer :: kfrag
      integer :: next_pivot_state ! The state index of the next step's pivot state
      integer :: flag_state_number_change ! Flag to indicate a change in the number of states b/w steps
      integer :: flag_read_MOs  ! Flag to indicate if it is safe to read MO coefficients from file from previous step
      integer :: nBecomeUnavailableNextHop ! Helper counter for keeping track of which oxygens are unavailable

      double precision :: dx
      double precision :: dy
      double precision :: dz
      double precision :: dd

      integer :: count
      integer :: i
      integer :: j
      integer :: k
      integer :: s
      integer :: f

      integer :: n
      integer :: ME, MASTER, NPROC, IBTYP, IPTIM
      integer :: IR,IW,IP,IS,IPK,IDAF,NAV,IODA
      logical :: GOPARR, DSKWRK, MASWRK

      COMMON /PAR   / ME,MASTER,NPROC,IBTYP,IPTIM,GOPARR,DSKWRK,MASWRK

      natoms = nat
      MAX_SPACE = MAX_STATES*natoms

      if(.not.allocated(fragment)) allocate(fragment(1:MAX_SPACE))
      if(.not.allocated(charges)) allocate(charges(1:MAX_SPACE))
      if(.not.allocated(old_fragment))
     $        allocate(old_fragment(1:MAX_SPACE))
      if(.not.allocated(hop_array)) allocate(hop_array(1:MAX_SPACE))
      if(.not.allocated(available)) allocate(available(1:natoms))
      if(.not.allocated(environment)) allocate(environment(1:natoms))
      if(.not.allocated(init_charges))
     $        allocate(init_charges(1:nfrg))
      if(.not.allocated(init_fragment))
     $        allocate(init_fragment(1:natoms))
      if(.not.allocated(coord)) allocate(coord(1:natoms*3))
      if(.not.allocated(element)) allocate(element(1:natoms))
      if(.not.allocated(toBecomeUnavailableNextHop))
     $   allocate( toBecomeUnavailableNextHop (1:MAX_SPACE) )

      charges(:)=0
      coord(:) = coord1d(:)
      element(:) = atom_name(:)
      init_fragment(:) = irmddat(:)
      init_charges(:) = irmdchg(:)

!     Array of init_charges is not the same size as charges

      do i=1,nfrg
        charges(i)=init_charges(i)
      enddo

      nprevstates = nstates
      fragmentIndexChange = 0
      next_pivot_state = 0

      nextstate = 1
      hop = 0
      hop_length = 1
      nstates = 1
      state = 0
      ireactive = 1
      nfragments = nfrg
      nBecomeUnavailableNextHop = 0

!     Find the reactive fragment (fragment consisting of 4 atoms)
!     If no reactive fragment is found the default will be set to 1

      do f = 1, nfrg
	count=0
        do i = 1, natoms
	  ifrag = init_fragment(i)
	  if (ifrag == f) then
	    count = count + 1
	  endif
        enddo
	if (count == 4) then
	  ireactive=f
	  exit
	endif
      enddo

! Initialize availability and reactive fragment
! Here, we assume that state zero is the pivot state

      do i = 1, natoms

         fragment(i) = init_fragment(i)

         available(i) = 1       ! Default all to be available
         hop_array(i) = 0       ! Default to zero, pivot state

!         write (6,*) '  This is atom number = ', i
!         write (6,*) '  This is atom index  = ', element(i)
!         write (6,*) '  This atom belongs to fragment  = ', fragment(i)

         ifrag = fragment(i)

! Check if fragment is reactive

         if (ifrag == ireactive) then
            reactive(i) = 1     ! Fragment reactive
            if (element(i) == 8) then
               available(i) = 0 ! Hydronium oxygen is unavailable to accept
            endif
         else
            reactive(i) = 0     ! Fragment not reactive
         endif
      enddo

      do i = 1, MAX_SPACE
         old_fragment(i) = fragment(i)
      enddo

! *** Breadth first search ***
! Loop until one of following is met:
! 1. Exhausted all possible donations
! 2. Maximum hops reached
! 3. Maximum states reached

      do while ( state<nextstate .and. state<MAX_STATES .and.
     $            hop<MAX_HOPS )

! Loop through atoms of state and attempt to donate
! hydronium H's to all possible O's within cutoff that are available
         do i = 1, natoms

            if ( element(i)==1 .and. available(i)==1 .and.
     &           reactive(state*natoms+i)==1 ) then

               do j = 1, natoms

                  if ( element(j)==8 .and. available(j)==1
     &               .and. reactive(state*natoms+j)==0 ) then
                     dx = coord(3*(i-1)+1) - coord(3*(j-1)+1)
                     dy = coord(3*(i-1)+2) - coord(3*(j-1)+2)
                     dz = coord(3*(i-1)+3) - coord(3*(j-1)+3)
                     dd = dx*dx + dy*dy + dz*dz

                     if ( dd < cut_OH2) then
              ! Copy current state to next state, setting acceptor fragment
              ! as the hydronium fragment in the next state
                        do k = 1, natoms
               fragment(nstates*natoms+k)  = fragment(state*natoms+k)
              !add one hop
               hop_array(nstates*natoms+k) = hop_array(state*natoms+k)+1
               hop_length = hop_length+1

               if ( fragment(state*natoms+k) ==
     $              fragment(state*natoms+j) ) then
                  reactive(nstates*natoms+k) = 1
               else
                  reactive(nstates*natoms+k) = 0
               endif
                        enddo

              ! Swap fragment index of donated H atom and set to hydronium
              jfrag = fragment(nextstate*natoms+i)
              fragment(nextstate*natoms+i) = fragment(state*natoms+j)
              kfrag = fragment(state*natoms+j)
              charges(jfrag+nfrg*nextstate)=0
              charges(kfrag+nfrg*nextstate)=1
              reactive(nextstate*natoms+i) = 1

              ! New way to handle bifurcation
              ! Oxygens remain available until we move on to the next hop
              nBecomeUnavailableNextHop = nBecomeUnavailableNextHop + 1
              toBecomeUnavailableNextHop(nBecomeUnavailableNextHop) = j

              nstates = nstates+1
              nextstate = nextstate+1

                     endif
                  endif
               enddo
        available(i) = 0 ! Make donated H atom unavailable for donation now
            endif
         enddo

    ! Move on to next state
         state = state+1
         if (hop_length > state) then
            hop = hop_array(state*natoms+1)

            do k = 1, nBecomeUnavailableNextHop
        ! Make tagged oxygens unavailable now
               available( toBecomeUnavailableNextHop(k) ) = 0

            enddo
      ! Clear the vector for next round
            nBecomeUnavailableNextHop = 0

         endif

      enddo ! close while loop

! ** Figure out which atoms belong to the environment **
! Those which are environment fragments will be approximated as being equal to that
! of the pivot state to save on computation time.

      do i = 1, natoms
         environment(i) = 1
         do s = 1, nstates
            if ( reactive((s-1)*natoms+i) == 1 ) then
               environment(i)=0
               exit
            endif
         enddo
      enddo

      if(maswrk) then

      write ( 6, '(a)' )    '  State search done'
      write ( 6, '(a,i3)' ) '  Final state hop: ', hop
      write ( 6, '(a,i3)' ) '  Number of states found: ', nstates

      do i = 1, natoms
         write ( 6, '(i4,f15.10,f15.10,f15.10,$)' )
     &   element(i), coord((i-1)*3+1),coord((i-1)*3+2),coord((i-1)*3+3)
         do s = 1, nstates
            write( 6, '(i3,$)' ) fragment((s-1)*natoms + i)
         enddo
         write( 6, '(a)' ) ' '
      enddo
      endif

      irmddat(:) = fragment(:)
      irmdchg(:) = charges(:)

      deallocate ( fragment )
      deallocate ( charges )
      deallocate ( old_fragment )
      deallocate ( available )
      deallocate ( hop_array )
      deallocate ( environment )
      deallocate ( init_charges )
      deallocate ( init_fragment )
      deallocate ( coord )
      deallocate ( toBecomeUnavailableNextHop )

      if(maswrk) write ( 6, '(a)' ) '  end of state function '
      return
      end
